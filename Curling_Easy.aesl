<!DOCTYPE aesl-source>
<network>


<!--list of global events-->


<!--list of constants-->
<constant value="0" name="FIND_START"/>
<constant value="1" name="MOVING"/>
<constant value="2" name="POSITION"/>
<constant value="3" name="HOLD"/>
<constant value="300" name="TARGET"/>
<constant value="300" name="THRESHOLD_BLACK"/>
<constant value="700" name="TURNTIMING"/>
<constant value="200" name="THRESHOLD_GREY"/>
<constant value="4" name="STOPPED"/>


<!--show keywords state-->
<keywords flag="true"/>


<!--node thymio-II-->
<node nodeId="1" name="thymio-II">var sixty = 0
var state = HOLD
motor.left.target = 0
motor.right.target = 0
call leds.circle(32,32,32,32,32,32,32,32)


#STOP-Routine to stop an light up circle
sub stop
	motor.left.target = 0
	motor.right.target = 0
	state = STOPPED
	timer.period[1] = 0
	timer.period[0] = 0
	sixty = 0
	call leds.circle(32,32,32,32,32,32,32,32)

#STOP-Event for the Robot
onevent button.backward
	callsub stop
	
#START-Routine of the robot, turns of circle and starts 60 sec timer
onevent button.forward
	call leds.circle(0,0,0,0,0,0,0,0)
	motor.left.target = TARGET
	motor.right.target = -TARGET
	state = FIND_START
	timer.period[0] = TURNTIMING #Timer for right turn
	timer.period[1] = 10000 #60-seconds timer

#PROX-Event to sense start and finish
onevent prox
	#Find the right line to start from there
	if  state == FIND_START then
		if  prox.ground.delta[0] &lt; THRESHOLD_BLACK then
			motor.left.target = -TARGET
			motor.right.target = TARGET
			state = POSITION
			timer.period[0] = TURNTIMING
		end
	end
	#go on until the circle is sensed
	if  state == MOVING then
		if  prox.ground.delta[0] &lt; THRESHOLD_BLACK then
			timer.period[0] = 300
			state = HOLD
		end
	end
	
	
#TIMER-Event to help robot adjust
onevent timer0
	if  state == FIND_START then
		motor.left.target = TARGET
		motor.right.target = TARGET
		timer.period[0] = 0
	end
	if  state == POSITION then
		motor.left.target = TARGET *19/20
		motor.right.target = TARGET
		timer.period[0] = 0
		state = MOVING
	end
	if  state == HOLD then
		callsub stop
	end

#TIMER-Event to make sure robot stops after 60 seconds
onevent timer1	
	if  sixty == 5 then
		callsub stop
	else
		sixty = sixty + 1
	end

</node>


</network>
